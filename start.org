#+TITLE: Emacs 24 configuration for Clinton Dreisbach
#+AUTHOR: Clinton Dreisbach
#+EMAIL: clinton@dreisbach.us
#+OPTIONS: toc:2 num:nil

* Introduction
** Standards
   Emacs does not have namespaces, so you should come up with some
   prefix for your variables and functions to avoid
   conflicts. Luckily, =/= is a valid character in Emacs symbols, so I
   emulate Clojure namespaces by using this. I use the prefix =cnd=
   (my initials), so a custom function I write would have a name like
   this: =cnd/gain-ultimate-power=.

** Inspirations
   - https://github.com/avdi/.emacs24.d
   - http://technomancy.us/153
   - http://pavpanchekha.com/blog/emacs.html
   - http://senny.github.com/cabbage/

* Personal Info
   Emacs will try and guess information about you from your
   environment. I prefer to be explicit about it.

#+BEGIN_SRC emacs-lisp
  (setq user-full-name "Clinton N. Dreisbach")
  (setq user-mail-address "clinton@dreisbach.us")
#+END_SRC

* Must-Have Configuration
  There are a few settings that any user of Emacs should have. You do
  want the power of =cl=, which brings in a lot of Common Lisp's
  methods.  You do want Emacs to reload changed files if you haven't
  also changed them in Emacs (=global-auto-revert-mode=). You want
  winner-mode, because you are a winner.

#+BEGIN_SRC emacs-lisp
  (require 'cl)
  (global-auto-revert-mode t)
  (winner-mode t)
#+END_SRC

* Functions
#+BEGIN_SRC emacs-lisp
  (defun cnd/clean-buffer-safe ()
    "Cleanup whitespace and make sure we are using UTF-8."
    (save-excursion
      (whitespace-cleanup-region (point-min) (point-max))
      (set-buffer-file-coding-system 'utf-8)))

  (defun cnd/clean-buffer ()
    "Re-indent the entire buffer and cleanup whitespace."
    (interactive)
    (save-excursion
      (indent-region (point-min) (point-max) nil)
      (cnd/clean-buffer-safe)))

  (defun cnd/rename-current-buffer-file ()
    "Renames current buffer and file it is visiting. From http://whattheemacsd.com/file-defuns.el-01.html."
    (interactive)
    (let ((name (buffer-name))
          (filename (buffer-file-name)))
      (if (not (and filename (file-exists-p filename)))
          (error "Buffer '%s' is not visiting a file!" name)
        (let ((new-name (read-file-name "New name: " filename)))
          (if (get-buffer new-name)
              (error "A buffer named '%s' already exists!" new-name)
            (rename-file filename new-name 1)
            (rename-buffer new-name)
            (set-visited-file-name new-name)
            (set-buffer-modified-p nil)
            (message "File '%s' successfully renamed to '%s'"
                     name (file-name-nondirectory new-name)))))))

  (defun cnd/kill-current-buffer-file ()
    "Removes file connected to current buffer and kills buffer. From http://whattheemacsd.com/file-defuns.el-02.html."
    (interactive)
    (let ((filename (buffer-file-name))
          (buffer (current-buffer))
          (name (buffer-name)))
      (if (not (and filename (file-exists-p filename)))
          (ido-kill-buffer)
        (when (yes-or-no-p "Are you sure you want to remove this file? ")
          (delete-file filename)
          (kill-buffer buffer)
          (message "File '%s' successfully removed" filename)))))

  (defun cnd/increase-font-size ()
    (interactive)
    (set-face-attribute 'default
                        nil
                        :height
                        (ceiling (* 1.10
                                    (face-attribute 'default :height)))))

  (defun cnd/decrease-font-size ()
    (interactive)
    (set-face-attribute 'default
                        nil
                        :height
                        (floor (* 0.9
                                  (face-attribute 'default :height)))))

  (defun cnd/save-buffer-always ()
    "Save the buffer even if it is not modified."
    (interactive)
    (set-buffer-modified-p t)
    (save-buffer))

  (defun cnd/edit-config ()
    (interactive)
    (find-file "~/.emacs.d/start.org"))

  (defun cnd/reload-config ()
    (interactive)
    (load-file "~/.emacs.d/init.el"))

  ;; All below from http://whattheemacsd.com/

  (defun cnd/move-line-down ()
    (interactive)
    (let ((col (current-column)))
      (save-excursion
        (forward-line)
        (transpose-lines 1))
      (forward-line)
      (move-to-column col)))

  (defun cnd/move-line-up ()
    (interactive)
    (let ((col (current-column)))
      (save-excursion
        (forward-line)
        (transpose-lines -1))
      (move-to-column col)))

  (defun cnd/open-line-below ()
    (interactive)
    (end-of-line)
    (newline)
    (indent-for-tab-command))

  (defun cnd/open-line-above ()
    (interactive)
    (beginning-of-line)
    (newline)
    (forward-line -1)
    (indent-for-tab-command))
#+END_SRC

* Package Management
** Package Repositories
   In addition to the default ELPA package repository, I use
   Marmalade, which is community-run and has new packages more
   quickly. I also use MELPA, which has up-to-date versions of
   packages built straight from their repos. I don't want to get all
   my packages from there, since they might be flaky, but if I want a
   particular package to use the HEAD version, I can throw it in the
   =package-archive-enable-alist=.

#+BEGIN_SRC emacs-lisp
  (load "package")

  (package-initialize)

  (add-to-list 'package-archives
               '("marmalade" . "http://marmalade-repo.org/packages/"))
  (add-to-list 'package-archives
               '("melpa" . "http://melpa.milkbox.net/packages/") t)

  (setq package-archive-enable-alist '(("melpa" deft magit autopair)))
#+END_SRC

** Packages to Install
  There's no way I can list each package I use and why here. The
  important thing to note is that I set up a var with a list of the
  packages I want to have installed.

#+BEGIN_SRC emacs-lisp
  (defvar cnd/packages '(adoc-mode
                         ag
                         auto-complete
                         autopair
                         cider
                         clojure-mode
                         coffee-mode
                         cperl-mode
                         deft
                         diminish
                         elisp-slime-nav
                         flymake
                         flymake-cursor
                         flymake-python-pyflakes
                         go-mode
                         god-mode
                         gist
                         haml-mode
                         htmlize
                         ido-ubiquitous
                         inf-mongo
                         jinja2-mode
                         magit
                         markdown-mode
                         marmalade
                         multiple-cursors
                         mustache-mode
                         noctilux-theme
                         org
                         paredit
                         phoenix-dark-mono-theme
                         phoenix-dark-pink-theme
                         pony-mode
                         projectile
                         python-mode
                         qsimpleq-theme
                         rainbow-delimiters
                         sass-mode
                         scss-mode
                         smex
                         yaml-mode)
    "Packages I always want installed.")
#+END_SRC

** Installing Packages
   This is the magic incantation to get your packages installed. It
   will require a network connection if you don't have all your
   packages currently installed, but otherwise, does not make any
   connections.

#+BEGIN_SRC emacs-lisp
  (defun cnd/packages-installed-p ()
    (loop for pkg in cnd/packages
          when (not (package-installed-p pkg)) do (return nil)
          finally (return t)))

  (unless (cnd/packages-installed-p)
    (message "%s" "Refreshing package database...")
    (package-refresh-contents)
    (dolist (pkg cnd/packages)
      (when (not (package-installed-p pkg))
        (package-install pkg))))
#+END_SRC
* Package Configuration
** Deft
   http://jblevins.org/projects/deft/

   Deft is an Emacs mode for quickly browsing, filtering, and editing
   directories of plain text notes, inspired by Notational Velocity. I
   like it a lot. I want it to store all my notes in my Dropbox, and I
   want it to use org-mode for those notes.

#+begin_src emacs-lisp
  (setq deft-directory "~/Notes/")
  (setq deft-use-filename-as-title t)
  (setq deft-extension "org")
  (setq deft-text-mode 'org-mode)
#+end_src

** Smex
   Smex is a replacement for the default behavior of =M-x= with better
   history and completion.

#+BEGIN_SRC emacs-lisp
  (setq smex-save-file (expand-file-name ".smex-items" user-emacs-directory))
  (smex-initialize)
#+END_SRC

** Ido
   Ido stands for Interactively Do Things, but it might as well stand
   for "Do the Right Thing." It makes Emacs have something resembling
   a modern interface with fuzzy-finding and autocompletion.

   =ido-ubiquitous= does exactly what you think it does: it takes the
   power of ido and lets you use it almost everywhere.

   I like to set the two options =ido-enable-flex-matching= and
   =ido-use-virtual-buffers=. Flex matching gives me fuzzy-finding,
   and using virtual buffers lets me switch to (and re-open) closed
   buffers from my buffer search.

#+BEGIN_SRC emacs-lisp
  (ido-mode t)
  (ido-ubiquitous t)
  (setq ido-enable-flex-matching t
        ido-use-virtual-buffers t)
#+END_SRC
** Ack
   =ack= is an enhanced grep.

#+BEGIN_SRC emacs-lisp
  (setq ack-prompt-for-directory t)
  (setq ack-executable (executable-find "ack-grep"))
#+END_SRC

** Magit

Instead of having magit run next to another window, I like having it
take up the whole frame, so I can take care of all my git stuff at
once and then it can go away. In order for this to work, I have to go
ahead and require magit first.

Source: http://whattheemacsd.com//setup-magit.el-01.html

#+begin_src emacs-lisp
  (require 'magit)

  (defadvice magit-status (around magit-fullscreen activate)
    (window-configuration-to-register :magit-fullscreen)
    ad-do-it
    (delete-other-windows))

  (defun magit-quit-session ()
    "Restores the previous window configuration and kills the magit buffer"
    (interactive)
    (kill-buffer)
    (jump-to-register :magit-fullscreen))

  (define-key magit-status-mode-map (kbd "q") 'magit-quit-session)
#+end_src

** Org-mode

Org-mode, the most useful thing in the world. This configuration 
file is in org-mode.

I want to be able to use [[http://ditaa.sourceforge.net/][ditaa]] and Clojure in my org-mode files.

#+BEGIN_SRC emacs-lisp
  (setq org-ditaa-jar-path "~/.emacs.d/vendor/ditaa0_9.jar")
  
  (org-babel-do-load-languages
   'org-babel-load-languages
   '((ditaa . t)))
  
  (require 'ob-clojure)
  
  (defvar org-babel-default-header-args:clojure 
    '((:exports . "code") (:results . "silent")))
  
  (declare-function nrepl-send-string-sync "ext:nrepl" (code &optional ns))
  
  (defun org-babel-execute:clojure (body params)
    "Execute a block of Clojure code with Babel."
    (require 'nrepl)
    (with-temp-buffer
      (insert (org-babel-expand-body:clojure body params))
      ((lambda (result)
         (let ((result-params (cdr (assoc :result-params params))))
           (if (or (member "scalar" result-params)
                   (member "verbatim" result-params))
               result
             (condition-case nil (org-babel-script-escape result)
               (error result)))))
       (plist-get (nrepl-send-string-sync
                   (buffer-substring-no-properties (point-min) (point-max))
                   (cdr (assoc :package params)))
                  :value))))
#+END_SRC

** God-mode

#+BEGIN_SRC emacs-lisp
  (global-set-key (kbd "<escape>") 'god-local-mode)
  (defun my-update-cursor ()
    (setq cursor-type (if (or god-local-mode buffer-read-only)
                          'box
                        'bar)))
  
  (add-hook 'god-mode-enabled-hook 'my-update-cursor)
  (add-hook 'god-mode-disabled-hook 'my-update-cursor)
#+END_SRC

* GUI Settings
  Emacs' default GUI settings are not great and cause it to not
  resemble a modern application. Luckily, they are very
  configurable.

#+BEGIN_SRC emacs-lisp
  (setq
   ;; Do not show a splash screen.
   inhibit-splash-screen t
   ;; Show incomplete commands while typing them.
   echo-keystrokes 0.1
   ;; Never show dialog boxes.
   use-dialog-box nil
   ;; Flash the screen on errors.
   visible-bell t)
  
  (setq-default
   ;; Make the cursor a thin vertical line.
   cursor-type 'bar
   ;; Show the end of files inside buffers.
   indicate-empty-lines t)
  
  ;; Show what text is selected.
  (transient-mark-mode t)
  ;; And delete selected text if we type over it.
  (delete-selection-mode t)
  
  ;; Always show matching sets of parentheses.
  (show-paren-mode t)
  
  ;; Highlight the current line.
  (global-hl-line-mode t)
  
  ;; Hide the scroll bar, tool bar, and menu bar.
  (scroll-bar-mode -1)
  (tool-bar-mode -1)
  (menu-bar-mode -1)
  
  ;; Allow us to type "y" or "n" instead of "yes" or "no".
  (defalias 'yes-or-no-p 'y-or-n-p)
  
  ;; Show the end of files.
  (when (not indicate-empty-lines)
    (toggle-indicate-empty-lines))
  
  (when window-system
    ;; Make the window title reflect the current buffer.
    (setq frame-title-format '(buffer-file-name "%f" ("%b")))
    ;; Load my favorite color theme. By passing t as the second parameter,
    ;; we are not prompted to set the theme as safe.
    (load-theme 'phoenix-dark-pink t))
#+END_SRC

** Modeline Improvements
   I like having the column number in my modeline to help with stack
   traces and just as a sanity check.

#+BEGIN_SRC emacs-lisp
  (setq column-number-mode t)
#+END_SRC

* Files and Directories
  I like to set up a vendor directory for any random elisp files I
  pull in. I get the majority of my functionality from packages
  distributed through ELPA or Marmalade, but sometimes, you find
  something random on EmacsWiki that you may want to pull in. After
  adding that directory, you're going to want to throw it in your
  load-path. Often, you'll have subdirectories in your vendor
  directory. Go ahead and add those to the load-path as well.

#+BEGIN_SRC emacs-lisp
  (defvar cnd/vendor-dir (expand-file-name "vendor" user-emacs-directory)
    "Location of any random elisp files I find from other authors.")
  (add-to-list 'load-path cnd/vendor-dir)

  (dolist (project (directory-files cnd/vendor-dir t "\\w+"))
    (when (file-directory-p project)
      (add-to-list 'load-path project)))
#+END_SRC

* Shells
  =C-d= on an empty line in the shell terminates the current
  process. Using the following, hitting =C-d= again will close the
  buffer.

#+begin_src emacs-lisp
  ;; From http://whattheemacsd.com/setup-shell.el-01.html

  (defun comint-delchar-or-eof-or-kill-buffer (arg)
    (interactive "p")
    (if (null (get-buffer-process (current-buffer)))
        (kill-buffer)
      (comint-delchar-or-maybe-eof arg)))

  (add-hook 'shell-mode-hook
            (lambda ()
              (define-key shell-mode-map
                (kbd "C-d") 'comint-delchar-or-eof-or-kill-buffer)))
#+end_src
* Text Editing
** Backups
   Default Emacs behavior around backups dates from a time of
   barbarism and should be avoided. On the other hand, it's saved me
   once or twice. Therefore, I allow Emacs to keep backups, but
   instead of keeping them in the same directory as the file being
   edited, I force them to be kept in a temporary directory.

#+BEGIN_SRC emacs-lisp
  (setq backup-directory-alist `((".*" . ,temporary-file-directory)))
  (setq auto-save-file-name-transforms `((".*" ,temporary-file-directory t)))
#+END_SRC

** File Behavior
   All files should end with a final newline.

#+BEGIN_SRC emacs-lisp
  (setq require-final-newline t)
#+END_SRC

** Indentation
   By default, I want to indent with 2 spaces and no tabs.

#+begin_src emacs-lisp
  (setq-default indent-tabs-mode nil
                tab-width 2)
#+end_src

** Multiple Buffers
I often have multiple buffers open with the same name. Emacs usually
deals with this by appending the buffer number to the buffer
name. This is hard to keep track of. Instead, the uniquify library
uses the parent directory name as a prefix to the file name.

#+BEGIN_SRC emacs-lisp
  (require 'uniquify)
  (setq uniquify-buffer-name-style 'forward)
#+END_SRC
** Multiple Cursors

Having multiple cursors is the new magic, and I'm still not used to
it. [[http://emacsrocks.com/e13.html][Check out the great video about it at Emacs Rocks.]]

#+begin_src emacs-lisp
  (require 'multiple-cursors)
  
  (global-set-key (quote [C-return]) 'set-rectangular-region-anchor)
  (global-set-key (kbd "C-S-c C-S-c") 'mc/edit-lines)
  (global-set-key (kbd "C->") 'mc/mark-next-like-this)
  (global-set-key (kbd "C-<") 'mc/mark-previous-like-this)
  (global-set-key (kbd "C-c C->") 'mc/mark-all-like-this)
#+end_src

** Zap to Char
Zap to char is very useful, but zapping up to a character is even more
useful. A method to do this is in =misc.el=.

#+begin_src emacs-lisp
  (require 'misc)
#+end_src

** Markdown
Make sure that all Markdown file extensions trigger =markdown-mode=.

#+BEGIN_SRC emacs-lisp
  (add-to-list 'auto-mode-alist '("\\.md$" . markdown-mode))
  (add-to-list 'auto-mode-alist '("\\.mdown$" . markdown-mode))
#+END_SRC

Also use =visual-line-mode= to get decent word-wrap when in
=markdown-mode=.

#+BEGIN_SRC emacs-lisp
  (add-hook 'markdown-mode-hook (lambda () (visual-line-mode t)))
#+END_SRC

I use =pandoc= for converting Markdown, so I want to set that as the
command to use when exporting from =markdown-mode=. I want to enable
smart quotes. I use [[http://kevinburke.bitbucket.org/markdowncss/][markdown.css]] to beautify the output.

#+BEGIN_SRC emacs-lisp
  (setq markdown-command "pandoc --smart -f markdown -t html")
  (setq markdown-css-path (expand-file-name "markdown.css" cnd/vendor-dir))
#+END_SRC

* Programming
** autopair-mode
   =autopair-mode= automatically adds closing parentheses, brackets,
   and the like, which is very useful when writing code. It doesn't
   get auto-loaded, so I have to require it explicitly.

#+BEGIN_SRC emacs-lisp
  (require 'autopair)
#+END_SRC

** C
   C has its own indentation rules, so I have to set them with a
   separate variable.
#+begin_src emacs-lisp
  (setq c-basic-offset 2)
#+end_src

** Clojure
   I want Emacs to open up ClojureScript files with Clojure syntax.

   I want paredit-mode when I am editing Clojure files as well as
   rainbow delimiters.

   Lastly, =cider= is the Emacs client for the Clojure REPL, and I
   want to configure it to hide its special buffers, use eldoc, and be
   easy to switch to.

#+BEGIN_SRC emacs-lisp
  (add-to-list 'auto-mode-alist '("\\.cljs$" . clojure-mode))
    
  (add-hook 'clojure-mode-hook 'paredit-mode)
  (add-hook 'clojure-mode-hook 'rainbow-delimiters-mode)
  (add-hook 'clojure-mode-hook 'subword-mode)
  (add-hook 'cider-repl-mode-hook 'paredit-mode)
  (add-hook 'cider-repl-mode-hook 'rainbow-delimiters-mode)
  (add-hook 'cider-repl-mode-hook 'subword-mode)
    
  (add-hook 'cider-mode-hook 'cider-turn-on-eldoc-mode)
  (setq nrepl-hide-special-buffers t)
  (add-to-list 'same-window-buffer-names "*nrepl*")
#+END_SRC

** CSS/SCSS
   CSS has its own indentation rules, so I have to set them with a
   separate variable.

   I do not want SCSS to automatically compile to CSS upon save.

#+begin_src emacs-lisp
  (setq css-indent-offset 2
        scss-compile-at-save nil)
#+end_src

** Emacs Lisp
Give us the power of SLIME when coding in elisp; specifically, allow
us to go to definition with M-. and back again with M-,.

#+begin_src emacs-lisp
  (autoload 'elisp-slime-nav-mode "elisp-slime-nav")
  (add-hook 'emacs-lisp-mode-hook (lambda () (elisp-slime-nav-mode t)))
  (eval-after-load 'elisp-slime-nav '(diminish 'elisp-slime-nav-mode))
#+end_src

** Go
   Go is a rad systems language from Google. One thing I like about it
   is that it has exquisite tools built for it. In particular, it has
   =gofmt=, which formats the code according to Google's specs. I am
   adding a hook to =go-mode= to turn on =autopair-mode= and to run
   =gofmt= before saving a file.

   I am also adding =go-autocomplete=, which will allow me to complete
   on methods from my own code and the Go libraries I am using.

#+BEGIN_SRC emacs-lisp
  (add-hook 'go-mode-hook
            (lambda ()
              (autopair-mode)
              (add-hook 'before-save-hook 'gofmt-before-save nil t)))

  (require 'go-autocomplete)
  (require 'auto-complete-config)
#+END_SRC

** Hy
   Hy is a Lisp implemented on top of Python. It is rad.

#+BEGIN_SRC emacs-lisp
  (require 'hy-mode)
  (add-hook 'hy-mode-hook 'paredit-mode)
  (add-hook 'hy-mode-hook 'rainbow-delimiters-mode)
#+END_SRC

** Javascript
   JS has its own indentation rules, so I have to set them with a
   separate variable.

#+begin_src emacs-lisp
  (setq js-indent-level 2)
#+end_src

** Lisp
   Lisp is the most awesome family of programming languages ever. When
   I'm using Lisp, I want ultimate power, so I turn on paredit, which
   gives me amazing abilities to move code around inside
   S-expressions.

   This section is a good example of how to extend Emacs with keymaps
   and minor-modes.

#+BEGIN_SRC emacs-lisp
  ;; lisp.el
  ;; (setq lisp-modes '(lisp-mode
  ;;                    emacs-lisp-mode
  ;;                    common-lisp-mode
  ;;                    scheme-mode
  ;;                    clojure-mode))
  ;; 
  ;; (defvar lisp-power-map (make-keymap))
  ;; (define-minor-mode lisp-power-mode "Fix keybindings; add power."
  ;;   :lighter " (power)"
  ;;   :keymap lisp-power-map
  ;;   (paredit-mode t))
  ;; (diminish 'lisp-power-mode)
  ;; (define-key lisp-power-map [delete] 'paredit-forward-delete)
  ;; (define-key lisp-power-map [backspace] 'paredit-backward-delete)
  ;; 
  ;; (defun cnd/engage-lisp-power ()
  ;;   (lisp-power-mode t))
  ;; 
  ;; (dolist (mode lisp-modes)
  ;;   (add-hook (intern (format "%s-hook" mode))
  ;;             #'cnd/engage-lisp-power))
  
#+END_SRC

** Python
I have customized pychecker to run my own version, which runs
autopep8, pyflakes, and pep8.

#+begin_src emacs-lisp
  (setq py-pychecker-command "~/.emacs.d/vendor/pychecker"
        py-pychecker-command-args '("")
        python-check-command "~/.emacs.d/vendor/pychecker")
#+end_src

*** Django

I use =[[https://github.com/davidmiller/pony-mode][pony-mode]]= to enhance Emacs when I'm working with Django. I
have it vendored, since it's not on Marmalade. I have to require it to
get =pony-tpl-mode= (a minor mode for Django templates) working, which
I should figure out.

#+begin_src emacs-lisp
  ;; TODO autoload this instead of using require

  (require 'pony-mode)
#+end_src

** Ruby
   There are a lot of different file names and extensions that are associated with Ruby. I've listed all I can think of so that =ruby-mode= is loaded for each of them.

#+BEGIN_SRC emacs-lisp
  ;; Rake files are ruby, too, as are gemspecs, rackup files, etc.
  (add-to-list 'auto-mode-alist '("\\.rake$" . ruby-mode))
  (add-to-list 'auto-mode-alist '("\\.gemspec$" . ruby-mode))
  (add-to-list 'auto-mode-alist '("\\.ru$" . ruby-mode))
  (add-to-list 'auto-mode-alist '("Rakefile$" . ruby-mode))
  (add-to-list 'auto-mode-alist '("Gemfile$" . ruby-mode))
  (add-to-list 'auto-mode-alist '("Capfile$" . ruby-mode))
  (add-to-list 'auto-mode-alist '("Vagrantfile$" . ruby-mode))
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (add-hook 'ruby-mode-hook
            (lambda ()
              (autopair-mode)
              (add-hook 'before-save-hook 'whitespace-cleanup nil t)
              (define-key ruby-mode-map "{" 'self-insert-command)
              (define-key ruby-mode-map "}" 'self-insert-command)
              (define-key ruby-mode-map (kbd "RET") 'newline-and-indent)))
#+END_SRC
   
** Scheme

#+BEGIN_SRC emacs-lisp
  (add-hook 'scheme-mode-hook 'paredit-mode)
  (add-hook 'scheme-mode-hook 'rainbow-delimiters-mode)
  
  ;; From chicken scheme wiki
  
  (require 'cmuscheme)
  
  (setq scheme-program-name "csi -:c")
  
  ;; Indenting module body code at column 0
  (defun scheme-module-indent (state indent-point normal-indent) 0)
  (put 'module 'scheme-indent-function 'scheme-module-indent)
  
  (put 'and-let* 'scheme-indent-function 1)
  (put 'parameterize 'scheme-indent-function 1)
  (put 'handle-exceptions 'scheme-indent-function 1)
  (put 'when 'scheme-indent-function 1)
  (put 'unless 'scheme-indent-function 1)
  (put 'match 'scheme-indent-function 1)
  (put 'pmatch 'scheme-indent-function 1)
  
  (define-key scheme-mode-map "\C-c\C-l" 'scheme-load-current-file)
  (define-key scheme-mode-map "\C-c\C-k" 'scheme-compile-current-file)
  
  (defun scheme-load-current-file (&optional switch)
    (interactive "P")
    (let ((file-name (buffer-file-name)))
      (comint-check-source file-name)
      (setq scheme-prev-l/c-dir/file (cons (file-name-directory    file-name)
             (file-name-nondirectory file-name)))
      (comint-send-string (scheme-proc) (concat "(load \""
                  file-name
                  "\"\)\n"))
      (if switch
        (switch-to-scheme t)
        (message "\"%s\" loaded." file-name) ) ) )
  
  (defun scheme-compile-current-file (&optional switch)
    (interactive "P")
    (let ((file-name (buffer-file-name)))
      (comint-check-source file-name)
      (setq scheme-prev-l/c-dir/file (cons (file-name-directory    file-name)
             (file-name-nondirectory file-name)))
      (message "compiling \"%s\" ..." file-name)
      (comint-send-string (scheme-proc) (concat "(compile-file \""
                  file-name
                  "\"\)\n"))
      (if switch
        (switch-to-scheme t)
        (message "\"%s\" compiled and loaded." file-name))))
  
  ;; scheme-complete
  
  (autoload 'scheme-smart-complete "scheme-complete" nil t)
  (eval-after-load 'scheme
    '(define-key scheme-mode-map "\t" 'scheme-complete-or-indent))
  
  (autoload 'scheme-get-current-symbol-info "scheme-complete" nil t)
  (add-hook 'scheme-mode-hook
    (lambda ()
      (make-local-variable 'eldoc-documentation-function)
      (setq eldoc-documentation-function 'scheme-get-current-symbol-info)
      (eldoc-mode)))
#+END_SRC
** YAML
#+BEGIN_SRC emacs-lisp
  (add-to-list 'auto-mode-alist '("\\.yml$" . yaml-mode))
  (add-to-list 'auto-mode-alist '("\\.yaml$" . yaml-mode))
#+END_SRC
* Keybindings
  Keybindings are a really personal matter. My philosophy with Emacs
  is this: keep as many keybindings as possible the same as the
  out-of-the-box Emacs config, and use =C-c= for your own special
  preferences. It's cool to extend the default functionality (setting
  Return to =newline-and-indent= instead of =newline=, for example),
  but radically altering it is only going to frustrate you when you
  have use Emacs without your configuration. Plus, it's nice to anyone
  you ever have to pair program with.

#+BEGIN_SRC emacs-lisp
  (global-set-key (kbd "RET") 'newline-and-indent)
  (global-set-key (kbd "C-;") 'comment-or-uncomment-region)
  (global-set-key (kbd "M-/") 'hippie-expand)
  (global-set-key (kbd "M-x") 'smex)
  (global-set-key (kbd "M-X") 'smex-major-mode-commands)
  (if window-system (global-unset-key (kbd "C-z")))

  (global-set-key (kbd "C-+") 'cnd/increase-font-size)
  (global-set-key (kbd "C-=") 'cnd/increase-font-size)
  (global-set-key (kbd "C--") 'cnd/decrease-font-size)
#+END_SRC

** Super-Special Personal Keybindings

#+BEGIN_SRC emacs-lisp
  (global-set-key (kbd "C-c a") 'mark-whole-buffer)
  (global-set-key (kbd "C-c c") 'query-replace-regexp)
  (global-set-key (kbd "C-c d") 'deft)
  (global-set-key (kbd "C-c g") 'magit-status)
  (global-set-key (kbd "C-c M-k") 'cnd/kill-current-buffer-file)
  (global-set-key (kbd "C-c n") 'cnd/clean-buffer)
  (global-set-key (kbd "C-c q") 'join-line)
  (global-set-key (kbd "C-c r") 'revert-buffer)
  (global-set-key (kbd "C-c C-r") 'cnd/rename-current-buffer-file)
  (global-set-key (kbd "C-c s e") 'cnd/edit-config)
  (global-set-key (kbd "C-c s r") 'cnd/reload-config)
  (global-set-key (kbd "C-c C-s") 'cnd/save-buffer-always)
  (global-set-key (kbd "C-c v") 'eval-buffer)
  (global-set-key (kbd "C-c w") 'whitespace-mode)
  (global-set-key (kbd "C-c x") 'execute-extended-command)
  (global-set-key (kbd "C-c z") 'zap-to-char)
  (global-set-key (kbd "M-Z") 'zap-to-char)
  (global-set-key (kbd "M-z") 'zap-up-to-char)
  (global-set-key (kbd "<C-S-down>") 'cnd/move-line-down)
  (global-set-key (kbd "<C-S-up>") 'cnd/move-line-up)
  (global-set-key (kbd "<C-return>") 'cnd/open-line-below)
  (global-set-key (kbd "<C-S-return>") 'cnd/open-line-above)
 
  (global-set-key (kbd "M-j")
            (lambda ()
                  (interactive)
                  (join-line -1)))
#+END_SRC

** Windmove
   Windmove is a nice little feature to let you move between open
   windows by pressing =Shift= + the arrow keys. I like to add the
   ability to also do this using =C-c=, as using =Shift= may not work
   on all terminals.

#+BEGIN_SRC emacs-lisp
  (windmove-default-keybindings 'shift)
  (global-set-key (kbd "C-c <left>")  'windmove-left)
  (global-set-key (kbd "C-c <right>") 'windmove-right)
  (global-set-key (kbd "C-c <up>")    'windmove-up)
  (global-set-key (kbd "C-c <down>")  'windmove-down)
#+END_SRC
* Emacs Server
  It is very annoying to find that you cannot close a buffer that has
  been opened via =emacsclient= in the same way you would close any
  other buffer. The following code fixes that.

#+BEGIN_SRC emacs-lisp
  (add-hook 'server-switch-hook
            (lambda ()
              (when (current-local-map)
                (use-local-map (copy-keymap (current-local-map))))
              (when server-buffer-clients
                (local-set-key (kbd "C-x k") 'server-edit))))
#+END_SRC
* OS-Specific Settings
** OS X
   Emacs works pretty great on OS X, but there are a few adjustments I
   like. =mdfind= is a command-line interface to Spotlight which works
   great when using the =locate= function in Emacs. =aspell= is used
   instead of =ispell=. Lastly, OS X has annoying issues with the
   =PATH= environment variable. If you launch Emacs as an app instead
   of on the command-line, it will not have the path you set up in
   =.profile= (or wherever you set up your environment variables.)
   Therefore, I set it explicitly.

#+BEGIN_SRC emacs-lisp
    (when (eq system-type 'darwin)
      (setq locate-command "mdfind")
      (setq ispell-program-name "aspell")
      
      (defun set-exec-path-from-shell-PATH ()
        (let ((path-from-shell (shell-command-to-string "$SHELL -i -c 'echo $PATH'")))
          (setenv "PATH" path-from-shell)
          (setq exec-path (split-string path-from-shell path-separator))))
  
      (when window-system (set-exec-path-from-shell-PATH))
  
      (set-face-attribute 'default nil
                    :family "Ubuntu Mono" :height 180 :weight 'normal)
      (setq mac-option-key-is-meta t)
      (setq mac-command-key-is-meta t)
      (setq mac-command-modifier 'meta)
      (setq mac-option-modifier 'meta)
      (global-set-key (kbd "M-+") 'cnd/increase-font-size)
      (global-set-key (kbd "M-=") 'cnd/increase-font-size)
      (global-set-key (kbd "M--") 'cnd/decrease-font-size))
    
#+END_SRC
* Local Configuration
  No matter how universal your configuration is, you will find
  yourself someday needing to configuration something different for a
  specific computer. I keep that code in =local.el=, which gets loaded
  at the end of my main configuration.

#+BEGIN_SRC emacs-lisp
  (let ((local-config (expand-file-name "local.el" user-emacs-directory)))
    (when (file-exists-p local-config)
      (load local-config)))
#+END_SRC
